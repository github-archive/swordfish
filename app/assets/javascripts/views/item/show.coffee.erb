#= require ZeroClipboard

Item.Views ?= {}

class Item.Views.Show extends Backbone.View
  template: 'templates/item/show'

  events:
    'click a.reveal': 'reveal'
    'click a.hide':   'hide'

  afterRender: ->
    @model.select()
    @clipboard = new ZeroClipboard @$('#copy_link')[0], moviePath: "<%= asset_path 'ZeroClipboard.swf' %>"
    @clipboard.on 'dataRequested', (client, args) =>
      @clipboard.setText @model.data().password

  serialize: ->
    _.extend @model.toJSON(), data: @model.data()

  reveal: (e) =>
    e.preventDefault()
    @$('.password').text(@model.data().password)
    @$el.addClass('revealed')

  hide: (e) =>
    e.preventDefault()
    @$('.password').text('••••••••••••')
    @$el.removeClass('revealed')
